plugins {
    id 'com.github.kt3k.coveralls' version '2.12.0'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

// Apply the java plugin to add support for Java
apply plugin: 'java'

archivesBaseName = 'invoiced'
group = 'com.invoiced'

task fatJar(type: Jar) {
    archivesBaseName = project.name + '-all'
    with jar
}

configurations.all {
}

repositories {
    mavenCentral()
}

version = '4.3.0'

// In this section you declare the dependencies for your production and test code
dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.9'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.9'
    implementation 'org.json:json:20160212'
    implementation 'org.apache.httpcomponents:httpasyncclient:4.1.1'
    implementation 'org.apache.httpcomponents:httpclient:4.5.2'
    implementation 'org.apache.httpcomponents:httpmime:4.5.2'
    implementation 'com.mashape.unirest:unirest-java:1.4.9'
    implementation 'com.auth0:java-jwt:3.3.0'

    testImplementation "com.github.tomakehurst:wiremock:2.1.12"


    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testImplementation 'junit:junit:4.12'
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

coveralls {
    jacocoReportPath "build/reports/jacoco/test/jacocoTestReport.xml"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'invoiced'
                description = 'Official Java client library for the Invoiced API'
                url= 'https://github.com/Invoiced/invoiced-java'

                scm {
                    url = 'https://github.com/Invoiced/invoiced-java'
                    connection = 'scm:https://github.com/invoiced/invoiced-java.git'
                    developerConnection = 'scm:git://github.com/invoiced/invoiced-java.git'
                }

                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'invoiced'
                        name = 'Invoiced Inc'
                        email = 'support@invoiced.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.findProperty("nexusUsername")
                password = project.findProperty("nexusPassword")
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
